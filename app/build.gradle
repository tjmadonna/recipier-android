plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-parcelize"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "androidx.navigation.safeargs"
    id "com.google.android.libraries.mapsplatform.secrets-gradle-plugin"
}

android {
    namespace "com.inelasticcollision.recipelink"

    compileSdkVersion 33

    defaultConfig {
        applicationId "com.inelasticcollision.recipelink"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 25
        versionName "3.0.5"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // View binding
        buildFeatures {
            viewBinding true
        }

        // Room
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }

        // Room
        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }

        signingConfigs {
            release {
                if (rootProject.file("release.properties").exists()) {
                    // Import signing keys
                    def keystorePropertiesFile = rootProject.file("release.properties")
                    def keystoreProperties = new Properties()
                    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                    storeFile rootProject.file(keystoreProperties["ReleaseStoreFile"])
                    storePassword keystoreProperties["ReleaseStorePassword"]
                    keyAlias keystoreProperties["ReleaseKeyAlias"]
                    keyPassword keystoreProperties["ReleaseKeyPassword"]
                    v1SigningEnabled true
                    v2SigningEnabled true
                }
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/recipier_debug"]
            minifyEnabled false
        }
        release {
            manifestPlaceholders = [appName: "@string/recipier_release"] // Set release app name
            minifyEnabled true // Enable code shrinking, obfuscation, and optimization
            shrinkResources true // Enable resource shrinking

            // Default ProGuard rules files that are packaged with the Android Gradle plugin
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release // Set signing
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }

    // Include the testShared directory in both the test and androidTest
    sourceSets {
        test {
            java.srcDirs += "$projectDir/src/testShared"
        }

        androidTest {
            java.srcDirs += "$projectDir/src/testShared"
        }
    }

    // Java 8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // For Kotlin
    kotlinOptions {
        jvmTarget = "1.8"
    }

    kapt {
        correctErrorTypes = true
    }
}

dependencies {
    implementation fileTree(include: ["*.jar"], dir: "libs")

    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "com.google.android.material:material:1.7.0"

    // Multidex
    implementation "androidx.multidex:multidex:2.0.1"

    // Kotlin coroutines
    def coroutinesVersion = "1.6.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    // Coil
    implementation "io.coil-kt:coil:2.1.0"
    implementation "io.coil-kt:coil-gif:2.1.0"

    // Lifecycle components
    def lifecycleVersion = "2.5.1"
    def archVersion = "2.1.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    testImplementation "androidx.arch.core:core-testing:$archVersion"

    // Room
    def roomVersion = "2.4.3"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    // Moshi
    def moshiVersion = "1.13.0"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    // Retrofit
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    androidTestImplementation "androidx.navigation:navigation-testing:$navVersion"

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"

    // Dependencies for local unit tests
    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test:core-ktx:1.4.0"
    implementation "androidx.test.ext:junit-ktx:1.1.3"
    testImplementation "org.robolectric:robolectric:4.8.2"
    testImplementation "org.mockito:mockito-core:4.6.1"

    // Android Testing Support Library"s runner and rules
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"

    // Espresso UI Testing dependencies.
    def espressoVersion = "3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
}

// Secrets plugin
secrets {
    defaultPropertiesFileName = "local.defaults.properties"
}